plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
}

apply from: "${sharedGradleScriptsDir}/fcli-module.gradle"

tasks.withType(JavaCompile) {
    dependsOn 'generateProto'
}

dependencies {
    implementation(project(":fcli-core:fcli-common"))

    implementation("org.yaml:snakeyaml:2.2")

    implementation("jakarta.xml.bind:jakarta.xml.bind-api:2.3.3")
    implementation("org.glassfish.jaxb:jaxb-runtime:2.3.3")
    implementation("com.auth0:java-jwt:4.4.0")

    implementation("io.grpc:grpc-netty-shaded:1.69.0")
    implementation("io.grpc:grpc-protobuf:1.69.0")
    implementation("io.grpc:grpc-stub:1.69.0")
    compileOnly("org.apache.tomcat:annotations-api:6.0.53")

    implementation("com.google.protobuf:protobuf-java:4.28.3")
    implementation(files(fileTree(protobuf.generatedFilesBaseDir).matching { include "**/*.java" }))
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:4.28.3"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.69.0"
        }
    }
    generateProtoTasks {
        all().forEach { task ->
            task.plugins {
                grpc {} // Enables gRPC stub generation
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs += "${protobuf.generatedFilesBaseDir}/main/grpc"
            srcDirs += "${protobuf.generatedFilesBaseDir}/main/java"
        }
    }
}
